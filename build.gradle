apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
group = 'com.athaydes.easy-jetty'
version = '0.1'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}

repositories {
    mavenCentral()
}

def jettyVersion = '7.6.16.v20140903'

dependencies {
    compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile group: 'org.eclipse.jetty.aggregate', name: 'jetty-servlet', version: jettyVersion

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.9'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0'
    testCompile group: 'org.eclipse.jetty.aggregate', name: 'jetty-client', version: jettyVersion
    testCompile group: 'io.fastjson', name: 'boon', version: '0.32'
}

task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

/* Publishing config */

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    publications = [ "mavenJava" ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'easy-jetty'
        licenses = [ 'Apache-2.0' ]
        desc = 'Makes it really easy to embed a Jetty Web Server'
        websiteUrl = 'https://github.com/renatoathaydes/easy-jetty'
        issueTrackerUrl = 'https://github.com/renatoathaydes/easy-jetty/issues'
        labels = [ 'http', 'webserver', 'jetty', 'java' ]
        publicDownloadNumbers = true
    }
}

bintrayUpload.dependsOn build, sourcesJar
